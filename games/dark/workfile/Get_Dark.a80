	device zxspectrum48
	ORG	24576

PROGBEGIN
PROGRUN
	LD	HL,#5800
	LD	DE,#5801
	LD	BC,#2ff
	LD	(HL),0
	LDIR

	LD	HL,QLOGO
	CALL	SPACK_A

	LD	HL,#5800+32*14
	LD	DE,#5800+32*14+1
	LD	BC,32*9-1
	LD	(HL),2
	LDIR


	LD	HL,GUI
	LD	DE,16384
	LD	BC,674
	LDIR

	LD	BC,#0E07
	LD	DE,OLEG
	CALL	QPRINT
	LD	BC,#100B
	LD	DE,RERE
	CALL	QPRINT
	LD	BC,#120a
	LD	DE,ALF
	CALL	QPRINT
	LD	BC,#1607
	LD	DE,WWW
	CALL	QPRINT

	JR $

	RET

OLEG	DB	127," 1997 OLEG ORIGIN",0
RERE	DB	"Re-release",0
WWW	DB	"www.olegorigin.org",0
ALF	db 	"ALF  version",0

SPACK_A	LD	E,(HL)
	LD	A,E
	CP	255
	JR	Z,SPATR_A
	INC	HL
	LD	D,(HL)
	INC	HL

	PUSH	HL
	LD	A,D
SPACKY	ADD	A,8		; (VAR) Y pos

	CALL	3742
	LD	A,E
	ADD	A,L
	LD	L,A

	EX	DE,HL
	POP	HL

	LD	A,(HL)
	INC	HL
	LD	B,A
SPKA1	PUSH	DE

	DUP 8
	LD	A,(HL)
	LD	(DE),A
	INC	D
	INC	HL
	EDUP

	POP	DE
	INC	E
	DJNZ	SPKA1
	JR	SPACK_A

SPATR_A	LD	DE,#5800+256
	INC	HL
	LD	C,(HL)
	INC	HL
	LD	B,(HL)
	INC	HL

	; version for width = 32 only

SPKA5	PUSH	BC
	LD	B,4
SPKA6
 .8	LDI
	DJNZ	SPKA6
	POP	BC
	DJNZ	SPKA5
	RET


SYSFONT	EQU	15616-256
FONT	DEFW	SYSFONT	; place user font address-256 here

QPRINT	LD	A,(DE)
	AND	A
	RET	Z
	PUSH	DE
	CALL	PRINTCHAR
	POP	DE
	INC	DE
	JR	QPRINT
PRINTCHAR
	EXA
	LD	A,B
	CALL	3742
	LD	A,C
	ADD	A,L
	LD	L,A
	INC	C
	PUSH	BC
	LD	D,H
	LD	E,L
	EXA	
	LD	L,A
	LD	H,0
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	LD	BC,(FONT)
	ADD	HL,BC
	CP	"w"
	JR	Z,CHAR_NORMAL
	CP	"i"
	JR	Z,CHAR_NORMAL
	CP	"I"
	JR	Z,CHAR_SPECIAL_I
	CP	"1"
	JR	Z,CHAR_SPECIAL_I
	CALL	CHAR_SPECIAL1
RET_I	POP	BC
	RET

CHAR_NORMAL
	LD	B,8
PRCHNRM	LD	A,(HL)
	LD	(DE),A
	INC	D
	INC	HL
	DJNZ	PRCHNRM
	JR	RET_I

CHAR_SPECIAL_I
	LD	A,00001100b
	LD	(CHAR_I+1),A
	CALL	CHAR_SPECIAL1
	LD	A,00001111b
	LD	(CHAR_I+1),A
	JR	RET_I

CHAR_SPECIAL1
	LD	B,8
PRCHSP1	LD	A,(HL)
	AND	11110000b
	SRL	A
	LD	C,A
	LD	A,(HL)
CHAR_I	AND	00001111b
	OR	C
	LD	(DE),A
	INC	D
	INC	HL
	DJNZ	PRCHSP1
	RET

QLOGO
	INCBIN	"res\logo.dat"

GUI
	INCBIN	"gui.dat"

	SAVESNA "Get_Dark.sna",PROGRUN
	SAVEBIN "Get_Dark.bin",PROGBEGIN,$-PROGBEGIN
