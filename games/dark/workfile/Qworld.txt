WORLD_NEW

	IFN STLEVEL=0
	XOR	A
	LD	(STLVSET+1),A
	ENDIF

	CALL	CLEARGAMEWIN
	CALL	CLEARKEY4

	LD	DE,TX6498+3
	IF LANGUAG=0
	LD	BC,#0008
	ENDIF
	IF LANGUAG=1
	LD	BC,#000A
	ENDIF
	CALL	PRINT
	LD	DE,TXEND
	CALL	PRINT

	CALL	WORLD_SHARED
	LD	DE,WORLDDATA_RD2
	LD	BC,#060D
	CALL	PRINTA

	LD	DE,WORLDDATA_TREE
	LD	BC,#0707
	CALL	PRINTX
	LD	BC,#0609
	CALL	PRINTX
	LD	BC,#0819
	CALL	PRINTX
	LD	BC,#0912
	CALL	PRINTX

	EI
	LD	B,4
PIKPIK	PUSH	BC
	CALL	FXCLICK
	LD	B,25
DELAYH	HALT
	DJNZ	DELAYH
	POP	BC
	DJNZ	PIKPIK

	LD	IX,DOMA
	LD	B,13
PRDOMA	PUSH	BC
	LD	C,(IX)
	LD	B,(IX+1)
	INC	IX
	INC	IX
	LD	DE,WORLDDATA_HOUSE
	CALL	PRINTX
	SLA	C
	SLA	C
	LD	B,#88
	CALL	FXBEEP
	CALL	FXCLICK
	POP	BC
	DJNZ	PRDOMA

	LD	B,88
PIKPIK2	PUSH	BC
	LD	C,B
	LD	B,22
	CALL	FXBEEP
	POP	BC
	DJNZ	PIKPIK2

	LD	HL,QFONT-256-8*16
	LD	(FONT),HL
	CALL	FIREPRESSLOOP

	LD	DE,TXCLEAR+3
	LD	BC,#0200
	JP	PRINT

DOMA	DEFW	#031A
	DEFW	#0303
	DEFW	#080A
	DEFW	#0502
	DEFW	#0C07
	DEFW	#061A
	DEFW	#0814
	DEFW	#0803
	DEFW	#0E05
	DEFW	#0906
	DEFW	#060E
	DEFW	#0A1A
	DEFW	#090F

WORLD	CALL	CLEARGAMEWIN

	LD	A,(LEVEL)
	AND	A
	RET	Z

	LD	A,(LEVEL)
	LD	BC,#0009
	CALL	PRINTNUM
	IF LANGUAG=0
	LD	BC,#0003
	ENDIF
	IF LANGUAG=1
	LD	BC,#0001
	ENDIF
	LD	DE,TXLEVEL
	CALL	PRINTX
	LD	A,(LEVEL)
	LD	B,A
PLVNAM2	LD	A,(DE)
	INC	DE
	AND	A
	JR	Z,PLVNAM1
	JR	PLVNAM2
PLVNAM1	DJNZ	PLVNAM2
	LD	BC,#000E
	CALL	PRINT
	LD	BC,#000C
	LD	DE,WDEFIS
	CALL	PRINT

	CALL	WORLD_SHARED
	
	LD	DE,WORLDDATA_MAZE
	LD	BC,#0A04
	CALL	PRINTX
	LD	BC,#060C
	INC	DE
	INC	DE
	LD	A,3+64
	CALL	PRINTA
	LD	DE,WORLDDATA_MAZE+2
	LD	BC,#0817
	LD	A,2
	CALL	PRINTA

	LD	DE,WORLDDATA_ALLEY
	LD	BC,#0607
	CALL	PRINTX
	LD	BC,#0307
	CALL	PRINTX

	LD	DE,WORLDDATA_NEST
	LD	BC,#021B
	LD	A,5
	CALL	PRINTA
	LD	DE,WORLDDATA_NEST+3
	LD	BC,#0319
	LD	A,5
	CALL	PRINTA
	LD	DE,WORLDDATA_NEST+8
	LD	BC,#0418
	LD	A,5
	CALL	PRINTA

	CALL	FIRERELEASE

	LD	DE,ARROWLEVPOS
	LD	A,(LEVEL)
	DEC	A
	ADD	A,A
	LD	H,0
	LD	L,A
	ADD	HL,DE
	LD	C,(HL)
	INC	HL
	LD	B,(HL)

	LD	HL,WARROWCOLORSEQ
	EI
WARROW	PUSH	BC
	INC	HL
	LD	A,(HL)
	AND	A
	JR	NZ,WARROW1
	LD	HL,WARROWCOLORSEQ
WARROW1	PUSH	HL
	LD	DE,WORLDDATA_ARROW
	CALL	PRINTA
	CALL	FIREPRESSED
	JR	NZ,WRLDEND
 .3	HALT
	POP	HL
	POP	BC
	JR	WARROW

WRLDEND	POP	HL
	POP	BC
	LD	HL,QFONT-256-8*16
	LD	(FONT),HL

	CALL	FXCLICK

	CALL	FIRERELEASE
	JP	CLEARLINE


WORLD_SHARED
	LD	HL,WFONT-256
	LD	(FONT),HL

	LD	DE,WORLDDATA_GROUND
	LD	BC,#0402
	CALL	DRWRLDG
	LD	BC,#0802
	CALL	DRWRLDG
	LD	BC,#0C02
	CALL	DRWRLDG

	XOR	A
	LD	IX,#5800+64*2
	LD	B,12
MBLKSID	LD	DE,32
	LD	(IX+1),A
	LD	(IX+29),A
	ADD	IX,DE
	DJNZ	MBLKSID

	LD	DE,WORLDDATA_RIVER
	LD	BC,#0411
	LD	A,6
WRIV1	PUSH	AF
	CALL	PRINTX
	INC	B
	INC	B
	POP	AF
	DEC	A
	JR	NZ,WRIV1

	LD	DE,WORLDDATA_SANCTUM
	LD	BC,#0B0A
	CALL	PRINTX
	LD	BC,#0513
	CALL	PRINTX

	LD	DE,WORLDDATA_RD2
	LD	BC,#0A05
	CALL	PRINTX
	LD	BC,#0605
	CALL	PRINTX
	LD	BC,#070D
	CALL	PRINTX
	LD	BC,#0818
	CALL	PRINTX
	LD	DE,WORLDDATA_RD1+7
	LD	BC,#0D02
	LD	A,6
	CALL	PRINTA
	LD	DE,WORLDDATA_RD1+4
	LD	BC,#0514
	LD	A,6
	CALL	PRINTA
	LD	DE,WORLDDATA_RD3
	LD	BC,#0505
	LD	A,6
	CALL	PRINTA
	LD	DE,WORLDDATA_RD1
	LD	BC,#0C0E
	LD	A,6
	CALL	PRINTA
	LD	DE,WORLDDATA_RD3+4
	LD	BC,#0714
	LD	A,6
	CALL	PRINTA

	LD	DE,WORLDDATA_BRIDGE
	LD	BC,#0D0F
	CALL	PRINTX

	LD	DE,WORLDDATA_HOUSE
	LD	BC,#0D17
	CALL	PRINTX
	LD	BC,#0C19
	CALL	PRINTX
	LD	BC,#0A15
	JP	PRINTX

WARROWCOLORSEQ
	DEFB	1,2,4,6+64,4,2,0

WDEFIS	DB	">",0

DRWRLDG	LD	A,14
DRWRLD1	PUSH	AF
	CALL	PRINTX
	INC	B
	INC	B
	DEC	C
	CALL	PRINTX
	DEC	B
	DEC	B
	INC	C
	INC	C
	INC	C
	POP	AF
	DEC	A
	JR	NZ,DRWRLD1
	RET

WORLDDATA_RD1
	DB	40,55,55,55,55,55,55,55,55,55,57,0
WORLDDATA_RD2
	DB	15,6,39,13,39,13,39,13,39,0
WORLDDATA_RD3
	DB	40,55,55,55,55,55,55,55,41,0
WORLDDATA_RIVER
	DB	42,13,58,13,0

WORLDDATA_TREE
	DB	15,4,38,13,15,7,54,0
WORLDDATA_BRIDGE
	DB	15,3,59,60,60,60,43,0
WORLDDATA_ALLEY
	DB	15,4,38,38,38,38,13,15,7,54,54,54,54,0
WORLDDATA_GROUND
	DB	15,1,32,33,13,48,49,0
WORLDDATA_HOUSE
	DB	15,2,45,46,13,15,7,61,62,0
WORLDDATA_MAZE
	DB	15,5,34,35,36,37,13,50,51,52,53,0
WORLDDATA_SANCTUM
	DB	15,6,40,55,55,57,13,39,15,4+64,61,62,13,15,6,56,55,55,55,57,0
WORLDDATA_NEST
	DB	47,63,0,47,63,63,63,0,47,63,60,63,63,0
WORLDDATA_ARROW
	DB	44,0

WFONT
	INCBIN	"res\world.dat"

ARROWLEVPOS
	DEFW	#0C06	; LEVEL 01
	DEFW	#0807	; LEVEL 02
	DEFW	#080E	; LEVEL 03
	DEFW	#0E0B	; LEVEL 04
	DEFW	#0E10	; LEVEL 05
	DEFW	#0F17	; LEVEL 06
	DEFW	#0A19	; LEVEL 07
	DEFW	#0814	; LEVEL 08
	DEFW	#061B	; LEVEL 09

/*

LEVEL 00: (PROLOGUE)
LEVEL 01: DARK MAZE
LEVEL 02: DEATH AVENUE
LEVEL 03: CORRIDORS OF FEAR
LEVEL 04: SANCTUM
LEVEL 05: BRIDGE OF DOOM
LEVEL 06: LOST VILLAGE
LEVEL 07: INFERNO
LEVEL 08: OBSERVATORY
LEVEL 09: EVIL NEST

DARK MAZE
DEATH AVENUE
CORRIDORS OF FEAR
SANCTUM
BRIDGE OF DOOM
LOST VILLAGE
INFERNO
OBSERVATORY
EVIL NEST

“≈ÃÕ€… À¿¡»–»Õ“
¿ÀÀ≈ﬂ —Ã≈–“»
 Œ–»ƒŒ–€ —“–¿’¿
—¬ﬂ“»À»Ÿ≈
ÃŒ—“ —”ƒ‹¡€
œŒ »Õ”“¿ﬂ ƒ≈–≈¬Õﬂ
¿ƒ
Œ¡—≈–¬¿“Œ–»ﬂ
√Õ≈«ƒŒ «À¿

*/
